/**
 * ALU (Arithmetic Logic Unit):
 * Computes out = one of the following functions:
 *                0, 1, -1,
 *                x, y, !x, !y, -x, -y,
 *                x + 1, y + 1, x - 1, y - 1,
 *                x + y, x - y, y - x,
 *                x & y, x | y
 * on the 16-bit inputs x, y,
 * according to the input bits zx, nx, zy, ny, f, no.
 * In addition, computes the two output bits:
 * if (out == 0) zr = 1, else zr = 0
 * if (out < 0)  ng = 1, else ng = 0
 */
// Implementation: Manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) sets x = 0        // 16-bit constant
// if (nx == 1) sets x = !x       // bitwise not
// if (zy == 1) sets y = 0        // 16-bit constant
// if (ny == 1) sets y = !y       // bitwise not
// if (f == 1)  sets out = x + y  // integer 2's complement addition
// if (f == 0)  sets out = x & y  // bitwise and
// if (no == 1) sets out = !out   // bitwise not

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute (out = x + y) or (out = x & y)?
        no; // negate the out output?
    OUT 
        out[16], // 16-bit output
        zr,      // if (out == 0) equals 1, else 0
        ng;      // if (out < 0)  equals 1, else 0

    PARTS:
    Mux16(a=x,b[0..15]=false,sel=zx,out=zdx); //Zero the x
    Not16(in=zdx,out=notx);                  //Not the x
    Mux16(a=zdx,b=notx,sel=nx,out=ndx);      //chose x or notx
    // ditto for y
    Mux16(a=y,b[0..15]=false,sel=zy,out=zdy);
    Not16(in=zdy,out=noty);
    Mux16(a=zdy,b=noty,sel=ny,out=ndy);

    Add16(a=ndx,b=ndy,out=xplusy); //x+y
    And16(a=ndx,b=ndy,out=xandy);  //x&y
    Mux16(a=xandy,b=xplusy,sel=f,out=fxy);  //chose function

    Not16(in=fxy,out=nfxy);      //not the output or not
    Mux16(a=fxy,b=nfxy,sel=no,out=oo);  //chose which


    Or16Way(in=oo,out=o);  //for zr
    Not(in=o,out=zr);

    And16(a[0..15]=true,b=oo,out[15]=ng,out[0..14]=drop); //ng

    Or16(a=oo,b[0..15]=false,out=out); //oo=output
}